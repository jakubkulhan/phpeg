-import . "common/list.phpeg"
-import . "common/parameters.phpeg"
-import . "common/id.phpeg"
-import . "common/whitespace.phpeg"
-import . "common/code.phpeg"

treewalker
  = __
    declarations:( "-namespace" _ namespace:join(id, "\\") __
                   -> array("namespace", $namespace)

                 / "-name" _ name:id __
                   -> array("name", $name)

                 / "-init" __ code:code __
                   -> array("init", $code)

                 / "-invoke" __ parameters:parameters? __ code:code __
                   -> array("invoke", $parameters, $code)

                 )*
    definitions:( nodematcher
                )+
    !.
    -> array("treewalker", array_merge($declarations, $definitions))

nodematcher
  = match:( "_" -> TRUE
          / list(id, __ "," __)
          )
    __ parameters:parameters? __ code:code __
    -> array("matcher", $match, (array) $parameters, $code)
